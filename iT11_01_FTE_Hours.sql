USE iTLoads;
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE VIEW dbo.iT11_01_FTE_Hours
AS
SELECT
	X.PER_REF_NO AS PER_REF_NO
	,'POSITION' AS [OBJECT]
	,CONVERT(char(8), A.CHANGE_DATE, 112) AS START_DATE
	,'FTE_HRS' AS [GROUP]
	,'FTE_HRS' AS [TYPE]
	,X.VALUE1
	,COALESCE(M.POS_REF_NO, '') AS POST_NO
FROM dbo.PERSON P
	JOIN dbo.APPOINTMENTS A
		ON P.EMP_NO = A.EMP_NO
	JOIN dbo.CONTRACT_DETAILS D
		ON A.EMP_NO = D.EMP_NO
			AND A.CONTRACT_NO = D.CONTRACT_NO
	LEFT JOIN dbo.MultiPaidContracts M
		ON A.EMP_NO = M.EMP_NO
			AND A.CONTRACT_NO = M.CONTRACT_NO
	CROSS APPLY
	(
		SELECT TOP 1 GR.STANDARD_HOURS
		FROM GRADES G
			LEFT JOIN GRADE_RANGE GR
				ON G.GRADE_GROUP = GR.SCALE_CODE
		WHERE G.GRADE_ID = A.GRADE
		ORDER BY GR.PAY_AWARD_DATE DESC
	) SH
	CROSS APPLY
	(
		VALUES
		(
			LTRIM(RTRIM(P.EMP_NO))
			,CASE
				WHEN SH.STANDARD_HOURS IS NULL
					AND A.GRADE IN ('AFC 7', 'AFC8C', 'AFC 5', 'AFC8B')
				THEN 37.5
				WHEN SH.STANDARD_HOURS IS NULL
					AND A.GRADE = 'PSC'
				THEN 0
				WHEN SH.STANDARD_HOURS IS NOT NULL
				THEN SH.STANDARD_HOURS
				ELSE 40
			END
		)
	) X (PER_REF_NO, VALUE1)
WHERE P.PERSON_STATUS NOT LIKE 'L%'
	AND X.VALUE1 <> 35
	AND D.IS_MASTER = 'Y'
	AND
	(
		A.CURRENT_INDICATOR IN ('C','L')
		OR
		(
			A.CURRENT_INDICATOR = 'J'
			AND
			(
				NOT EXISTS
				(
						SELECT 1
						FROM dbo.APPOINTMENTS A1
						WHERE A1.EMP_NO = A.EMP_NO
							AND A1.CURRENT_INDICATOR = 'C'		
				)
				OR A.CHANGE_DATE <= CURRENT_TIMESTAMP
			)
		)
	)
	AND EXISTS
	(
		SELECT 1
		FROM dbo.REFNO RN
		WHERE RN.PAYROLL_EMP_NO = P.EMP_NO
			AND RN.PAY_GROUP = 'MTH'
	);
GO
