USE iTLoads;
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
ALTER VIEW dbo.iT19_01_Inheritance_AVCs
--5.12.1 AVC Schemes
AS
WITH FilteredPensions
AS
(
	SELECT P.EMP_NO, X2.CONTRACT_NO, P.PENSION, P.PENSION_START, T.SchemeType, T.SchemeName
		,COALESCE(M.POS_REF_NO, '') AS POS_REF_NO
	FROM dbo.PENSION_CONDITION P
		JOIN dbo.TransPensions T
			ON P.PENSION = T.PENSION
		CROSS APPLY
		(
			VALUES (CASE WHEN ISNUMERIC(P.POST_NO) = 1 THEN P.POST_NO END)
		) X1 (CONTRACT_NO)
		LEFT JOIN dbo.CONTRACT_DETAILS D
			ON P.EMP_NO = D.EMP_NO
				AND X1.CONTRACT_NO = D.CONTRACT_NO
				AND D.IS_MASTER = 'Y'
		LEFT JOIN dbo.MultiPaidContracts M
			ON P.EMP_NO = M.EMP_NO
				AND X1.CONTRACT_NO= M.CONTRACT_NO
		CROSS APPLY
		(
			VALUES (COALESCE(D.CONTRACT_NO, M.CONTRACT_NO))
		) X2 (CONTRACT_NO)
	WHERE (P.PENSION_STOP IS NULL OR P.PENSION_STOP >= DATEADD(d, DATEDIFF(d, 0, CURRENT_TIMESTAMP), 0))
		AND X2.CONTRACT_NO IS NOT NULL
		-- 20170829 No USS Matched DC AVC Sal Sac, other AVCs in separate File
		AND T.SchemeType LIKE 'Additional%'
		AND P.PENSION <> 'USSMPP'
)
SELECT
	RTRIM(X.PER_REF_NO) AS PER_REF_NO
	,F.SchemeName AS SCHEME
	,CONVERT(char(8), F.PENSION_START, 112) AS START_DATE
	,'POSITION' AS [OBJECT]
	--,F.SchemeType AS NAME
	--,F.POS_REF_NO AS POST_NO
	--,'' AS PARENT_POST__NO
	--,'GTC' AS [GROUP]
FROM dbo.PERSON P
	CROSS APPLY
	(
		VALUES
		(
			--CASE
			--	WHEN LEN(P.EMP_NO) < 6
			--	THEN RIGHT('00000' + RTRIM(P.EMP_NO), 6)
			--	ELSE P.EMP_NO
			--END
			LTRIM(RTRIM(P.EMP_NO))
		)
	) X (PER_REF_NO)
	JOIN dbo.APPOINTMENTS A
		ON P.EMP_NO = A.EMP_NO
	JOIN FilteredPensions F
		ON A.EMP_NO = F.EMP_NO
			AND A.CONTRACT_NO = F.CONTRACT_NO
WHERE P.PERSON_STATUS NOT LIKE 'L%'
	AND
	(
		A.CURRENT_INDICATOR IN ('C','L')
		OR
		(
			A.CURRENT_INDICATOR = 'J'
			AND
			(
				NOT EXISTS
				(
						SELECT 1
						FROM dbo.APPOINTMENTS A1
						WHERE A1.EMP_NO = A.EMP_NO
							AND A1.CURRENT_INDICATOR = 'C'		
				)
				OR A.CHANGE_DATE <= CURRENT_TIMESTAMP
			)
		)
	)
	AND EXISTS
	(
		SELECT 1
		FROM dbo.REFNO RN
		WHERE RN.PAYROLL_EMP_NO = P.EMP_NO
			AND RN.PAY_GROUP = 'MTH'
	);
GO
