USE iTLoads;
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
ALTER VIEW dbo.iT19_00_Inheritance_Pension_Schemes
AS
WITH Pensions
-- ignore award contracts
AS
(
	SELECT P.EMP_NO, X2.CONTRACT_NO, P.PENSION, P.PENSION_START, T.SchemeType, T.SchemeName
		,COALESCE(M.POS_REF_NO, '') AS POS_REF_NO
	FROM dbo.PENSION_CONDITION P
		JOIN dbo.TransPensions T
			ON P.PENSION = T.PENSION
		CROSS APPLY
		(
			VALUES (CASE WHEN ISNUMERIC(P.POST_NO) = 1 THEN P.POST_NO END)
		) X1 (CONTRACT_NO)
		LEFT JOIN dbo.CONTRACT_DETAILS D
			ON P.EMP_NO = D.EMP_NO
				AND X1.CONTRACT_NO = D.CONTRACT_NO
				AND D.IS_MASTER = 'Y'
		LEFT JOIN dbo.MultiPaidContracts M
			ON P.EMP_NO = M.EMP_NO
				AND X1.CONTRACT_NO= M.CONTRACT_NO
		CROSS APPLY
		(
			VALUES (COALESCE(D.CONTRACT_NO, M.CONTRACT_NO))
		) X2 (CONTRACT_NO)
	WHERE (P.PENSION_STOP IS NULL OR P.PENSION_STOP >= DATEADD(d, DATEDIFF(d, 0, CURRENT_TIMESTAMP), 0))
		AND X2.CONTRACT_NO IS NOT NULL
		-- 20170829 No USS Matched DC AVC Sal Sac, other AVCs in separate File
		AND NOT T.SchemeType LIKE 'Additional%'
)
,FilteredPensions
AS
(
	SELECT EMP_NO, CONTRACT_NO, PENSION, PENSION_START, SchemeType, SchemeName, POS_REF_NO
	FROM Pensions P
	WHERE NOT EXISTS
	(
		SELECT 1
		FROM Pensions P1
		WHERE P1.EMP_NO = P.EMP_NO
			AND P1.CONTRACT_NO = P.CONTRACT_NO
			AND
			(
				(P.PENSION = 'USSCARE' AND P1.PENSION = 'USSCPP')
				OR
				(P.PENSION = 'USSDCAM' AND P1.PENSION = 'USSMPP')
				OR
				(P.PENSION = 'SAULCRB' AND P1.PENSION = 'SAULCPP')
			)
	)
)
SELECT
	RTRIM(X.PER_REF_NO) AS PER_REF_NO
	,'POSITION' AS [OBJECT]
	,CONVERT(char(8), F.PENSION_START, 112) AS START_DATE
	,'GTC' AS [GROUP]
	,F.SchemeType AS [TYPE]
	,F.SchemeName AS VALUE1
	,F.POS_REF_NO AS POST_NO
FROM dbo.PERSON P
	CROSS APPLY
	(
		VALUES
		(
			--CASE
			--	WHEN LEN(P.EMP_NO) < 6
			--	THEN RIGHT('00000' + RTRIM(P.EMP_NO), 6)
			--	ELSE P.EMP_NO
			--END
			LTRIM(RTRIM(P.EMP_NO))
		)
	) X (PER_REF_NO)
	JOIN dbo.APPOINTMENTS A
		ON P.EMP_NO = A.EMP_NO
	JOIN FilteredPensions F
		ON A.EMP_NO = F.EMP_NO
			AND A.CONTRACT_NO = F.CONTRACT_NO
WHERE P.PERSON_STATUS NOT LIKE 'L%'
	AND
	(
		A.CURRENT_INDICATOR IN ('C','L')
		OR
		(
			A.CURRENT_INDICATOR = 'J'
			AND
			(
				NOT EXISTS
				(
						SELECT 1
						FROM dbo.APPOINTMENTS A1
						WHERE A1.EMP_NO = A.EMP_NO
							AND A1.CURRENT_INDICATOR = 'C'		
				)
				OR A.CHANGE_DATE <= CURRENT_TIMESTAMP
			)
		)
	)
	AND EXISTS
	(
		SELECT 1
		FROM dbo.REFNO RN
		WHERE RN.PAYROLL_EMP_NO = P.EMP_NO
			AND RN.PAY_GROUP = 'MTH'
	);
GO
