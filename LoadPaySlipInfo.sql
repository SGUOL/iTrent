USE iTLoads;
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
ALTER PROCEDURE LoadPaySlipInfo
AS
SET NOCOUNT ON;

TRUNCATE TABLE dbo.PaySlipInfo;

WITH PR
AS
(
	SELECT MAX(PAYRUN_NO) AS PAYRUN_NO
	FROM dbo.PAYRUN_AUDIT
	WHERE PAYSLIP_TYPE = 'N'
)
INSERT INTO dbo.PaySlipInfo
SELECT
	X.PER_REF_NO
	,MAX(LTRIM(RTRIM(PSP.NI_CAT_CODE))) AS NI_CATEGORY
	,SUM(PSP.YTD_TAXABLE_PAY) AS TaxablePay
	,SUM(PSP.YTD_TAX) AS Tax
	,SUM(PSP.YTD_STUDENT_LOANS) AS SudentLoans
	,SUM(PSP.YTD_NI_EE_CONT) AS NI_EE_Contributions
	,SUM(PSP.YTD_NI_ER_CONT) AS NI_ER_Contributions
	--,PSP.YTD_NOT_CO_NIPAY AS NiablePay
	,SUM(YTD_NI_EE_EARN) AS NiablePay
	,SUM(PSP.YTD_EE_EARN_TO_LEL) AS NI2LEL
	,SUM(PSP.YTD_EE_EARN_LEL_TO_PT) AS NI2ET
	,SUM(PSP.YTD_EE_EARN_ST_TO_UEL) AS NI2UEL
	,SUM(PSP.YTD_PEN_PAY) AS PenPayDB
FROM dbo.PERSON P
	CROSS APPLY
	(
		VALUES
		(
			LTRIM(RTRIM(P.EMP_NO))
			--CASE
			--	WHEN LEN(P.EMP_NO) < 6
			--	THEN RIGHT('00000' + RTRIM(P.EMP_NO), 6)
			--	ELSE P.EMP_NO
			--END
		)
	) X (PER_REF_NO)
	CROSS JOIN PR
	JOIN dbo.APPOINTMENTS A
		ON P.EMP_NO = A.EMP_NO
	LEFT JOIN dbo.CONTRACT_DETAILS D
		ON A.EMP_NO = D.EMP_NO
			AND A.CONTRACT_NO = D.CONTRACT_NO
			AND D.IS_MASTER = 'Y'
	LEFT JOIN dbo.MultiPaidContracts M
		ON A.EMP_NO = M.EMP_NO
			AND A.CONTRACT_NO = M.CONTRACT_NO
	JOIN dbo.PAYSLIP_HISTORY PSP
		ON P.EMP_NO = PSP.EMP_NO
			AND PSP.PAYRUN_NO = PR.PAYRUN_NO
WHERE P.PERSON_STATUS NOT LIKE 'L%'
	AND (D.EMP_NO IS NOT NULL OR M.EMP_NO IS NOT NULL)
	AND A.SCALE_CODE IN ('CLINICAL','CLINICAL+','NON-STAFF','SGUL','SGUL PSC','CLINICAL++','AFC NEW','NEWCONSULT','SGUL TUPE')
	--Note these!!!!
	--AND NOT (A.SPINAL_POINT IS NULL OR A.GRADE IS NULL)
	AND
	(
		A.CURRENT_INDICATOR IN ('C','L')
		OR
		(
			A.CURRENT_INDICATOR = 'J'
			AND
			(
				NOT EXISTS
				(
						SELECT 1
						FROM dbo.APPOINTMENTS A1
						WHERE A1.EMP_NO = A.EMP_NO
							AND A1.CURRENT_INDICATOR = 'C'		
				)
				OR A.CHANGE_DATE <= CURRENT_TIMESTAMP
			)
		)
	)
	AND EXISTS
	(
		SELECT 1
		FROM dbo.REFNO RN
		WHERE RN.PAYROLL_EMP_NO = P.EMP_NO
			AND RN.PAY_GROUP = 'MTH'
	)
GROUP BY X.PER_REF_NO;

-- *** PaySlipPensions_RollUp
TRUNCATE TABLE dbo.PaySlipPensions;

WITH PR
AS
(
	SELECT MAX(PAYRUN_NO) AS PAYRUN_NO
	FROM dbo.PAYRUN_AUDIT
	WHERE PAYSLIP_TYPE = 'N'
)
,CPSPensions
AS
(
	SELECT SPH.EMP_NO, SPH.POST_NO
		,SPH.PENSION
		,SPH.YTD_EE_VALUE
		,SPH.YTD_ER_VALUE
	FROM dbo.PAYSLIP_PEN_HISTORY SPH
		JOIN PR
			ON SPH.PAYRUN_NO = PR.PAYRUN_NO
)
,FixedCPSPensions
AS
(
	SELECT P1.EMP_NO, P1.POST_NO, P1.PENSION
		,X.YTD_EE_VALUE
		,X.YTD_ER_VALUE
		,CASE
			WHEN P1.PENSION LIKE 'USS%'
			THEN ROUND((X.YTD_ER_VALUE * 100.0)/18, 2)
			WHEN P1.PENSION LIKE 'SAUL%'
			THEN ROUND((X.YTD_ER_VALUE * 100.0)/16, 2)
			WHEN P1.PENSION LIKE 'NHS%'
			THEN ROUND((X.YTD_ER_VALUE * 100.0)/14.38, 2)
			ELSE 0
		END AS PensionablePay
	FROM CPSPensions P1
		LEFT JOIN CPSPensions P2
			ON P2.EMP_NO = P1.EMP_NO
				AND P2.POST_NO = P1.POST_NO
				AND P2.PENSION =
					CASE
						WHEN P1.PENSION = 'USSCPP'
						THEN 'USSCARE'
						WHEN P1.PENSION = 'SAULCPP'
						THEN 'SAULCRB'
					END
		CROSS APPLY
		(
			VALUES
			(
				CASE
					WHEN P1.PENSION = 'USSMPP'
					THEN P1.YTD_ER_VALUE
					WHEN P2.YTD_ER_VALUE IS NOT NULL
					THEN P1.YTD_ER_VALUE
					ELSE P1.YTD_EE_VALUE
				END
				,CASE
					WHEN P1.PENSION = 'USSMPP'
					THEN 0
					WHEN P2.YTD_ER_VALUE IS NOT NULL
					THEN P2.YTD_ER_VALUE
					ELSE P1.YTD_ER_VALUE
				END
			)
		) X (YTD_EE_VALUE, YTD_ER_VALUE)
	WHERE NOT EXISTS
	(
		SELECT 1
		FROM CPSPensions P3
		WHERE P3.EMP_NO = P1.EMP_NO
			AND P3.POST_NO = P1.POST_NO
			AND
			(
				(P1.PENSION = 'USSCARE' AND P3.PENSION = 'USSCPP')
				OR
				(P1.PENSION = 'USSDCAM' AND P3.PENSION = 'USSMPP')
				OR
				(P1.PENSION = 'SAULCRB' AND P3.PENSION = 'SAULCPP')
			)
	) 
)
INSERT INTO dbo.PaySlipPensions
SELECT
	RTRIM(X.PER_REF_NO) AS PER_REF_NO
	,T.SchemeName
	,X.IsAVC
	,SUM(FP.YTD_EE_VALUE) AS YTD_EE_VALUE
	,SUM(FP.YTD_ER_VALUE) AS YTD_ER_VALUE
	,SUM(X.PensionablePayCALC) AS PensionablePayCALC
FROM dbo.PERSON P
	CROSS JOIN PR
	JOIN dbo.APPOINTMENTS A
		ON P.EMP_NO = A.EMP_NO
	LEFT JOIN dbo.CONTRACT_DETAILS D
		ON A.EMP_NO = D.EMP_NO
			AND A.CONTRACT_NO = D.CONTRACT_NO
			AND D.IS_MASTER = 'Y'
	LEFT JOIN dbo.MultiPaidContracts M
		ON A.EMP_NO = M.EMP_NO
			AND A.CONTRACT_NO = M.CONTRACT_NO
	JOIN FixedCPSPensions FP
		ON P.EMP_NO = FP.EMP_NO
	JOIN iTLoads.dbo.TransPensions T
		ON FP.PENSION = T.PENSION
	CROSS APPLY
	(
		VALUES
		(
			LTRIM(RTRIM(P.EMP_NO))
			--CASE
			--	WHEN LEN(P.EMP_NO) < 6
			--	THEN RIGHT('00000' + RTRIM(P.EMP_NO), 6)
			--	ELSE P.EMP_NO
			--END
			,CASE
				WHEN T.SchemeType LIKE 'Addition%'
				THEN 'Y'
				ELSE 'N'
			END
			,CASE
				WHEN T.SchemeName = 'NEST'
				THEN FP.YTD_ER_VALUE * 100
				ELSE FP.PensionablePay
			END
		)
	) X (PER_REF_NO, IsAVC, PensionablePayCALC)
WHERE P.PERSON_STATUS NOT LIKE 'L%'
	AND (D.EMP_NO IS NOT NULL OR M.EMP_NO IS NOT NULL)
	AND A.SCALE_CODE IN ('CLINICAL','CLINICAL+','NON-STAFF','SGUL','SGUL PSC','CLINICAL++','AFC NEW','NEWCONSULT','SGUL TUPE')
	--AND T.SchemeType LIKE 'Addition%'
	--AND NOT T.SchemeType LIKE 'Addition%'
	--Note these!!!!
	--AND NOT (A.SPINAL_POINT IS NULL OR A.GRADE IS NULL)
	AND
	(
		A.CURRENT_INDICATOR IN ('C','L')
		OR
		(
			A.CURRENT_INDICATOR = 'J'
			AND
			(
				NOT EXISTS
				(
						SELECT 1
						FROM dbo.APPOINTMENTS A1
						WHERE A1.EMP_NO = A.EMP_NO
							AND A1.CURRENT_INDICATOR = 'C'		
				)
				OR A.CHANGE_DATE <= CURRENT_TIMESTAMP
			)
		)
	)
	AND EXISTS
	(
		SELECT 1
		FROM dbo.REFNO RN
		WHERE RN.PAYROLL_EMP_NO = P.EMP_NO
			AND RN.PAY_GROUP = 'MTH'
	)
GROUP BY X.PER_REF_NO, T.SchemeName, X.IsAVC;
GO

