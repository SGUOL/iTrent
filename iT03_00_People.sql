USE iTLoads;
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
ALTER VIEW dbo.iT03_00_People
AS
SELECT 
	RTRIM(P.SURNAME) AS SURNAME
	,CASE
		WHEN X.FNSplitPos > 0
		THEN LEFT(P.FORENAMES, X.FNSplitPos -1)
		ELSE RTRIM(P.FORENAMES)
	END AS FORENAME1
	,CASE
		WHEN X.FNSplitPos > 0
		THEN LTRIM(RTRIM(SUBSTRING(P.FORENAMES, X.FNSplitPos +1, 80)))
		ELSE ''
	END AS FORENAME2
	,COALESCE(P.PREFERRED, '') AS PREFERRED_NAME
	,COALESCE(P.NI_NUMBER, '') AS SS_NO
	,CASE
		WHEN P.NI_NUMBER IS NULL
		THEN ''
		ELSE 'UK'
	END AS LEGISLATION
	,COALESCE(P.SEX, '') AS GENDER
	,COALESCE(RTRIM(P.MAIDEN_NAME), '') AS PREV_SURNAME
	,RTRIM(X.PER_REF_NO) AS PER_REF_NO
	,COALESCE(CONVERT(char(8), P.DATE_OF_START, 112), '') AS [START_DATE]
	-- Leave blank for load
	--,COALESCE(CONVERT(char(8), P.DATE_OF_LEAVING, 112), '') AS END_DATE
	,'' AS END_DATE
	,COALESCE(CONVERT(char(8), P.DATE_OF_BIRTH, 112), '') AS BIRTH_DATE
	-- Need to check!!! Undertaker?
	,COALESCE(CONVERT(char(8), P.DATE_OF_START, 112), '') AS CONT_REL_SERVICE_DATE
	-- Will need to sort out first 4 lines of address
	,CASE
		WHEN X.NoAddress = 0
		THEN RTRIM(COALESCE(P.ADDRESS_1, ''))
		ELSE 'St George''s, University of London'
	END AS ADDR_LINE_1
	,CASE
		WHEN X.NoAddress = 0
		THEN RTRIM(COALESCE(P.ADDRESS_2, ''))
		ELSE 'Cranmer Terrace'
	END AS ADDR_LINE_2
	,RTRIM(COALESCE(P.ADDRESS_3, '')) AS ADDR_LINE_3
	,CASE
		WHEN X.NoAddress = 0
		THEN RTRIM(COALESCE(P.TOWN, ''))
		ELSE 'LONDON'
	END AS ADDR_LINE_4
	-- ****
	,RTRIM(COALESCE(P.COUNTY, '')) AS ADDR_LINE_5
	,CASE
		WHEN X.NoAddress = 0
		THEN RTRIM(COALESCE(P.POSTCODE, ''))
		ELSE 'SW17 0RE'
	END AS ADDR_LINE_6
	,RTRIM(COALESCE(P.TELEPHONE, '')) AS CONTACT_NO
	,CASE
		WHEN LEN(X.Title) = 0 AND P.SEX = 'F'
		THEN 'Ms'
		WHEN LEN(X.Title) = 0
		THEN 'Mr'
		ELSE X.Title
	END AS TITLE
	,CASE
		WHEN P.COUNTRY IS NULL
		THEN 'United Kingdom'
		WHEN P.COUNTRY IN ('GBR', 'ENG', 'SCO', 'WAL', 'NI', '')
		THEN 'United Kingdom'
		WHEN XC.[DESCRIPTION] = 'Hong Kong'
		THEN 'China, Hong Kong Special Administrative Region'
		ELSE RTRIM(XC.[DESCRIPTION])
	END AS ADDRESS_COUNTRY
	,CASE
		WHEN P.ETHNIC_ORIGIN = '11'
		THEN 'White'
		WHEN P.ETHNIC_ORIGIN = '12'
		THEN 'White Irish'
		ELSE RTRIM(COALESCE(XE.[DESCRIPTION], P.ETHNIC_ORIGIN, ''))
	END AS ETHNIC_ORIGIN
	,RTRIM(COALESCE(XM.[DESCRIPTION], P.MARITAL_STATUS, '')) AS MARITAL_STATUS
FROM dbo.PERSON P
	LEFT JOIN dbo.PS_XREF XT
		ON P.TITLE = XT.CODE
			AND XT.[TYPE] = 'TITLE'
	LEFT JOIN dbo.PS_XREF XC
		ON P.COUNTRY = XC.CODE
			AND XC.[TYPE] = 'COUNTRY'
	LEFT JOIN dbo.PS_XREF XE
		ON P.ETHNIC_ORIGIN = XE.CODE
			AND XE.[TYPE] = 'HESA_ETHNIC'
	LEFT JOIN dbo.PS_XREF XM
		ON P.MARITAL_STATUS = XM.CODE
			AND XM.[TYPE] = 'MARITAL_STATUS'
	CROSS APPLY
	(
		VALUES
		(
			--CASE
			--	WHEN LEN(P.EMP_NO) < 6
			--	THEN RIGHT('00000' + RTRIM(P.EMP_NO), 6)
			--	ELSE P.EMP_NO
			--END
			RIGHT('00000' + LTRIM(RTRIM(P.EMP_NO)), 6)
			,CHARINDEX(' ', P.FORENAMES)
			,CASE
				WHEN LEN(COALESCE(P.ADDRESS_1,'')) = 0 AND LEN(COALESCE(P.ADDRESS_2,'')) = 0
					AND LEN(COALESCE(P.ADDRESS_3,'')) = 0 AND LEN(COALESCE(P.TOWN,'')) = 0
					AND LEN(COALESCE(P.COUNTY,'')) = 0 AND LEN(COALESCE(P.POSTCODE,'')) = 0
				THEN 1
				ELSE 0
			END
			,RTRIM(COALESCE(XT.[DESCRIPTION], P.TITLE, ''))
		)
	) X (PER_REF_NO, FNSplitPos, NoAddress, Title)
WHERE LEN(P.EMP_NO) < 7
	AND
	(
		P.PERSON_STATUS NOT LIKE 'L%'
		OR
		(P.PERSON_STATUS LIKE 'L%' AND P.DATE_OF_LEAVING >= '20170401')
	)

--SELECT
--	SURNAME, FORENAME1, FORENAME2, PREFERRED_NAME, SS_NO, LEGISLATION, GENDER, PREV_SURNAME
--	,PER_REF_NO
--	,[START_DATE], END_DATE, BIRTH_DATE, CONT_REL_SERVICE_DATE
--	,ADDR_LINE_1, ADDR_LINE_2, ADDR_LINE_3, ADDR_LINE_4, ADDR_LINE_5, ADDR_LINE_6
--	,CONTACT_NO, TITLE, ADDRESS_COUNTRY, ETHNIC_ORIGIN, MARITAL_STATUS
GO
