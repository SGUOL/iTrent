USE iTLoads;
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
ALTER VIEW dbo.iT15_01_Employment_Basis
AS
SELECT
	RTRIM(X.PER_REF_NO) AS PER_REF_NO
	,'POSITION' AS [OBJECT]
	,CONVERT(char(8), A.CHANGE_DATE, 112) AS START_DATE
	,'BASIS' AS [GROUP]
	,'CB' AS [TYPE]
	,CASE
		WHEN A.FULL_PART = 'FT'
		THEN 'Full Time'
		WHEN A.FULL_PART = 'PT'
		THEN 'Part Time'
		ELSE ''
	END AS VALUE1
	,COALESCE(M.POS_REF_NO, '') AS POST_NO
FROM dbo.PERSON P
	CROSS APPLY
	(
		VALUES
		(
			--CASE
			--	WHEN LEN(P.EMP_NO) < 6
			--	THEN RIGHT('00000' + RTRIM(P.EMP_NO), 6)
			--	ELSE P.EMP_NO
			--END
			LTRIM(RTRIM(P.EMP_NO))
		)
	) X (PER_REF_NO)
	JOIN dbo.APPOINTMENTS A
		ON P.EMP_NO = A.EMP_NO
	JOIN dbo.CONTRACT_DETAILS D
		ON A.EMP_NO = D.EMP_NO
			AND A.CONTRACT_NO = D.CONTRACT_NO
	LEFT JOIN dbo.MultiPaidContracts M
		ON A.EMP_NO = M.EMP_NO
			AND A.CONTRACT_NO = M.CONTRACT_NO
WHERE P.PERSON_STATUS NOT LIKE 'L%'
	AND A.FULL_PART = 'PT'
	AND D.IS_MASTER = 'Y'
	AND
	(
		A.CURRENT_INDICATOR IN ('C','L')
		OR
		(
			A.CURRENT_INDICATOR = 'J'
			AND
			(
				NOT EXISTS
				(
						SELECT 1
						FROM dbo.APPOINTMENTS A1
						WHERE A1.EMP_NO = A.EMP_NO
							AND A1.CURRENT_INDICATOR = 'C'		
				)
				OR A.CHANGE_DATE <= CURRENT_TIMESTAMP
			)
		)
	)
	AND EXISTS
	(
		SELECT 1
		FROM dbo.REFNO RN
		WHERE RN.PAYROLL_EMP_NO = P.EMP_NO
			AND RN.PAY_GROUP = 'MTH'
	);
GO
