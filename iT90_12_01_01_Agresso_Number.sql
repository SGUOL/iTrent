USE [iTLoads]
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE VIEW [dbo].[iT90_12_01_01_Agresso_Number]
AS
--12.1.1 User Defined Fields - File Formats
WITH ActiveResourceNos
AS
(
	SELECT DISTINCT R.RESOURCE_NO
	FROM dbo.PERSON P
		JOIN dbo.SGUL_AGRESSO_RESOURCE R
			ON P.EMP_NO = R.EMP_NO
	WHERE LEN(P.EMP_NO) < 7
		AND
		(
			P.PERSON_STATUS NOT LIKE 'L%'
			OR
			(P.PERSON_STATUS LIKE 'L%' AND P.DATE_OF_LEAVING >= '20170401')
		)
)
,People
AS
(
	SELECT P.*
		,ROW_NUMBER() OVER (PARTITION BY R.RESOURCE_NO ORDER BY COALESCE(P.DATE_OF_START, P.DATE_OF_LEAVING)) AS rn
	FROM dbo.SGUL_AGRESSO_RESOURCE R
		LEFT JOIN ActiveResourceNos A
			ON R.RESOURCE_NO = A.RESOURCE_NO
		JOIN dbo.PERSON P
			ON R.EMP_NO = P.EMP_NO
	WHERE A.RESOURCE_NO IS NULL
)
SELECT RIGHT('00000' + RTRIM(R.EMP_NO), 6) AS PER_REF_NO
	,'Agresso Number' AS [GROUP]
	,'Agresso Number' AS FIELD
	,CAST(R.RESOURCE_NO AS int) AS VALUE
	,'PERSON' AS ITEM_TYPE
FROM People P
	JOIN dbo.SGUL_AGRESSO_RESOURCE R
		ON P.EMP_NO = R.EMP_NO
WHERE LEN(P.EMP_NO) < 7
	AND P.FORENAMES IS NOT NULL
	AND P.rn = 1
	AND R.EMP_NO <> R.RESOURCE_NO
	AND P.PERSON_STATUS LIKE 'L%' AND P.DATE_OF_LEAVING < '20170401';
GO

