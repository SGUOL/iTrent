--Need to sort out BankNames from Sort Codes - Get translation table from Cata
USE iTLoads;
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
ALTER VIEW dbo.iT07_00_Payroll
AS
WITH TaxDetails
AS
(
	SELECT EMP_NO, RTRIM([SOURCE]) AS [SOURCE], TAX_CODE, TAX_BASIS, TAXABLE_PAY_PREV_EMP, TAX_PAID_PREV_EMP
		,ROW_NUMBER() OVER (PARTITION BY EMP_NO ORDER BY EFFECTIVE_DATE DESC) AS rn
	FROM dbo.DATED_TAX_CODE
	WHERE EFFECTIVE_DATE <= CURRENT_TIMESTAMP
)
SELECT
	RTRIM(X.PER_REF_NO) AS PER_REF_NO
	,RTRIM(X.PER_REF_NO) AS PAY_REF_NO
	,CASE
		WHEN P.DATE_OF_START < X.CURRENT_PAY_YEAR
		THEN CONVERT(char(8), X.CURRENT_PAY_YEAR, 112)
		ELSE CONVERT(char(8), P.DATE_OF_START, 112)
	END AS PAY_START_DATE
	,'UK' AS DEFAULT_LEGISLATION
	,CASE
		WHEN P.DATE_OF_START < X.CURRENT_TAX_YEAR
		THEN CONVERT(char(8), X.CURRENT_TAX_YEAR, 112)
		ELSE CONVERT(char(8), P.DATE_OF_START, 112)
	END AS TAX_EFF_DATE
	,RTRIM(T.TAX_CODE) AS TAX_CODE
	,T.TAX_BASIS
	,T.TAXABLE_PAY_PREV_EMP AS PREV_TAXABLE_PAY
	,T.TAX_PAID_PREV_EMP AS PREV_TAX_PAID
	,CASE
		WHEN P.DATE_OF_START < X.CURRENT_TAX_YEAR
		THEN CONVERT(char(8), X.CURRENT_TAX_YEAR, 112)
		ELSE CONVERT(char(8), P.DATE_OF_START, 112)
	END AS NI_EFF_DATE
	,RTRIM(C.NI_CAT_CODE) AS NI_CATEGORY
	,CASE
		WHEN RTRIM(XM.[DESCRIPTION]) = 'BACS'
		THEN 'BACS'
		ELSE 'Cash'
	END AS PAYMENT_METHOD
	,'Monthly' AS PAYROLL
	--,CASE
	--	WHEN P.DATE_OF_START >= X.CURRENT_TAX_YEAR
	--	THEN T.[SOURCE]
	--	ELSE ''
	--END AS TAX_CODE_SOURCE
	,CASE
		WHEN T.TAXABLE_PAY_PREV_EMP = 0
		THEN 'None'
		ELSE 'P6'
	END AS TAX_CODE_SOURCE
	,RTRIM(COALESCE(REPLACE(M.PAYMETH_BANK_SORT_CODE, '-', ''), '')) AS SORT_CODE
	,RTRIM(COALESCE(SCB.BankName, M.PAYMETH_BANK_NAME, '')) AS BANK_NAME
	,'' AS BANK_BRANCH_NAME
	,RTRIM(COALESCE(M.PAYMETH_ACCOUNT_NAME, '')) AS ACCOUNT_NAME
	,RTRIM(COALESCE(M.PAYMETH_ACCOUNT_NO, '')) AS ACCOUNT_NUMBER
	,RTRIM(COALESCE(M.PAYMETH_BSOC_ACCOUNT_NO, '')) AS ROLL_NUMBER
	,'0' AS ACCOUNT_TYPE
	--,CASE
	--	WHEN P.DATE_OF_START >= X.CURRENT_TAX_YEAR
	--	THEN 'T'
	--	ELSE 'F'
	--END AS HAS_P45_VL
	,'' AS HAS_P45_VL
	--,CASE
	--	WHEN P.DATE_OF_START >= X.CURRENT_TAX_YEAR
	--	THEN '1'
	--	ELSE ''
	--END AS P45_YR_VL
	,'' AS P45_YR_VL
	--,CASE
	--	WHEN T.[SOURCE] = 'P46'
	--		AND T.TAX_BASIS = 0
	--	THEN '1'
	--	WHEN T.[SOURCE] = 'P46'
	--		AND T.TAX_BASIS = 1
	--	THEN '2'
	--	WHEN T.[SOURCE] = 'P46'
	--		AND T.TAX_CODE = 'BR'
	--	THEN '3'
	--	WHEN T.[SOURCE] = 'P46(ER)'
	--	THEN '4'
	--	ELSE ''
	--END AS NEW_EMP_Q_VL
	,'' AS NEW_EMP_Q_VL
FROM dbo.PERSON P
	CROSS APPLY
	(
		VALUES
		(
			--CASE
			--	WHEN LEN(P.EMP_NO) < 6
			--	THEN RIGHT('00000' + RTRIM(P.EMP_NO), 6)
			--	ELSE P.EMP_NO
			--END
			LTRIM(RTRIM(P.EMP_NO))
			,CASE
				WHEN MONTH(CURRENT_TIMESTAMP) < 4
				THEN DATEADD(month, 3, DATEADD(year, YEAR(CURRENT_TIMESTAMP) - 1901, 0))
				ELSE DATEADD(month, 3, DATEADD(year, YEAR(CURRENT_TIMESTAMP) - 1900, 0))
			END
			,CASE
				WHEN MONTH(CURRENT_TIMESTAMP) < 4
					OR (MONTH(CURRENT_TIMESTAMP) = 4 AND DAY(CURRENT_TIMESTAMP) < 6)
				THEN DATEADD(day, 5, DATEADD(month, 3, DATEADD(year, YEAR(CURRENT_TIMESTAMP) - 1901, 0)))
				ELSE DATEADD(day, 5, DATEADD(month, 3, DATEADD(year, YEAR(CURRENT_TIMESTAMP) - 1900, 0)))
			END
		)
	) X (PER_REF_NO, CURRENT_PAY_YEAR, CURRENT_TAX_YEAR)
	JOIN dbo.APPOINTMENTS A
		ON P.EMP_NO = A.EMP_NO
	JOIN dbo.CONTRACT_DETAILS D
		ON A.EMP_NO = D.EMP_NO
			AND A.CONTRACT_NO = D.CONTRACT_NO
	JOIN TaxDetails T
		ON P.EMP_NO = T.EMP_NO
			AND T.rn = 1
	JOIN dbo.[CONTRACT] C
		ON A.EMP_NO = C.EMP_NO
			AND A.CONTRACT_NO = C.CONTRACT_NO
	JOIN dbo.EMPLOYEE_PAY_METHOD M
		ON P.EMP_NO = M.EMP_NO
	LEFT JOIN dbo.SortCodeXBank SCB
		ON M.PAYMETH_BANK_SORT_CODE = SCB.SortCode
	JOIN dbo.PS_XREF XM
		ON M.EMP_PAYMENT_METHOD = XM.CODE
			AND XM.[TYPE] = 'PAYMETHOD'
WHERE P.PERSON_STATUS NOT LIKE 'L%'
	AND D.IS_MASTER = 'Y'
	AND
	(
		A.CURRENT_INDICATOR IN ('C','L')
		OR
		(
			A.CURRENT_INDICATOR = 'J'
			AND
			(
				NOT EXISTS
				(
						SELECT 1
						FROM dbo.APPOINTMENTS A1
						WHERE A1.EMP_NO = A.EMP_NO
							AND A1.CURRENT_INDICATOR = 'C'		
				)
				OR A.CHANGE_DATE <= CURRENT_TIMESTAMP
			)
		)
	)
	AND EXISTS
	(
		SELECT 1
		FROM dbo.REFNO RN
		WHERE RN.PAYROLL_EMP_NO = P.EMP_NO
			AND RN.PAY_GROUP = 'MTH'
	);
GO
