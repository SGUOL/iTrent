USE iTLoads;
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
ALTER VIEW dbo.iT09_00_Work_Patterns
AS
WITH N1 AS ( SELECT * FROM ( VALUES (1),(1),(1),(1),(1),(1),(1) ) N(N) )
,N2 AS ( SELECT N1.N FROM N1, (VALUES (1),(1) ) N(N) )
,N(N) AS ( SELECT ROW_NUMBER() OVER (ORDER BY (SELECT NULL)) FROM N2 )
SELECT DISTINCT
	LTRIM(RTRIM(P.WORK_SHIFT_CODE_NEW)) AS PATTERN_NAME
	,RIGHT('0' + CAST(P.MaxWeek * 7 AS varchar(10)), 2) AS NUMBER_DAYS
	,CASE
		WHEN N.N < 10
		THEN '0' + CAST(N.N AS char(1))
		ELSE CAST(N.N AS char(2))
	END AS PATTERN_DAY_NUMBER
	,CASE N.N%7
		WHEN 1 THEN P.WORK_SHIFT_MONDAY
		WHEN 2 THEN P.WORK_SHIFT_TUESDAY
		WHEN 3 THEN P.WORK_SHIFT_WEDNESDAY
		WHEN 4 THEN P.WORK_SHIFT_THURSDAY
		WHEN 5 THEN P.WORK_SHIFT_FRIDAY
		WHEN 6 THEN P.WORK_SHIFT_SATURDAY
		WHEN 0 THEN P.WORK_SHIFT_SUNDAY
	END AS NUMBER_OF_HOURS
FROM dbo.WP_WSP P
	CROSS JOIN N
WHERE N.N <= P.MaxWeek * 7
--ORDER BY PATTERN_NAME, PATTERN_DAY_NUMBER
