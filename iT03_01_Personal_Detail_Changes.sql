USE iTLoads;
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
ALTER VIEW dbo.iT03_01_Personal_Detail_Changes
--3.2.1 Personal Details Changes
AS
SELECT 
	RTRIM(X.PER_REF_NO) AS PER_REF_NO
	,COALESCE(XD.[DESCRIPTION], 'No known disability') AS DISABILITY
	,COALESCE(XR.[DESCRIPTION], 'Information refused') AS RELIGION
	,COALESCE(XS.[DESCRIPTION], 'Information refused') AS SEXUAL_ORIENTATION
FROM dbo.PERSON P
	LEFT JOIN dbo.PS_XREF XD
		ON P.DISABILITY = XD.CODE
			AND XD.[TYPE] = 'HESA_DISABLE'
	LEFT JOIN dbo.PS_XREF XR
		ON P.RELIGION = XR.CODE
			AND XR.[TYPE] = 'HESA_RELBLF'
	LEFT JOIN dbo.PS_XREF XS
		ON P.SEXUAL_ORIENTATION = XS.CODE
			AND XS.[TYPE] = 'SEXUAL_ORIENTATION'
	CROSS APPLY
	(
		VALUES
		(
			RIGHT('00000' + LTRIM(RTRIM(P.EMP_NO)), 6)
		)
	) X (PER_REF_NO)
WHERE LEN(P.EMP_NO) < 7
	AND
	(
		P.PERSON_STATUS NOT LIKE 'L%'
		OR
		(P.PERSON_STATUS LIKE 'L%' AND P.DATE_OF_LEAVING >= '20170401')
	);
GO
