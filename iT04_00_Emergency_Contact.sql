USE iTLoads;
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
ALTER VIEW iT04_00_Emergency_Contact
AS
SELECT 
	RTRIM(X.PER_REF_NO) AS PER_REF_NO
	,COALESCE(RTRIM(P.EMERGENCY_CONTACT), 'Unknown') AS CONTACT_NAME
	,X1.DEFAULT_ADDRESS_I
	-- Only have one contact
	,'T' AS PRIMARY_CONTACT_I
	,RTRIM(X.CONTACT_NUMBER) AS CONTACT_NUMBER
	,'' AS CONTACT_EMAIL
	,CASE WHEN X1.DEFAULT_ADDRESS_I = 'F' THEN RTRIM(X.ADDR_LINE_1) ELSE '' END AS ADDR_LINE_1
	,CASE WHEN X1.DEFAULT_ADDRESS_I = 'F' THEN RTRIM(X.ADDR_LINE_2) ELSE '' END AS ADDR_LINE_2
	,CASE WHEN X1.DEFAULT_ADDRESS_I = 'F' THEN RTRIM(X.ADDR_LINE_3) ELSE '' END AS ADDR_LINE_3
	,CASE WHEN X1.DEFAULT_ADDRESS_I = 'F' THEN RTRIM(X.ADDR_LINE_4) ELSE '' END AS ADDR_LINE_4
	,CASE WHEN X1.DEFAULT_ADDRESS_I = 'F' THEN RTRIM(X.ADDR_LINE_5) ELSE '' END AS ADDR_LINE_5
	,CASE WHEN X1.DEFAULT_ADDRESS_I = 'F' THEN RTRIM(X.ADDR_LINE_6) ELSE '' END AS ADDR_LINE_6
	,'Unknown' AS RELATIONSHIP
	,CASE WHEN X1.DEFAULT_ADDRESS_I = 'F' THEN RTRIM(X.ADDRESS_COUNTRY) ELSE '' END AS ADDRESS_COUNTRY
FROM dbo.PERSON P
	LEFT JOIN dbo.PS_XREF XC
		ON P.COUNTRY = XC.CODE
			AND XC.[TYPE] = 'COUNTRY'
	CROSS APPLY
	(
		VALUES
		(
			--CASE
			--	WHEN LEN(P.EMP_NO) < 6
			--	THEN RIGHT('00000' + RTRIM(P.EMP_NO), 6)
			--	ELSE P.EMP_NO
			--END
			RIGHT('00000' + LTRIM(RTRIM(P.EMP_NO)), 6)
			,CASE
				WHEN P.EMERGENCY_CONTACT_TELEPHONE_NO IS NOT NULL
					AND P.EMERGENCY_MOBILE_TELEPHONE IS NOT NULL
				THEN RTRIM(P.EMERGENCY_CONTACT_TELEPHONE_NO) + '; ' + P.EMERGENCY_MOBILE_TELEPHONE
				WHEN P.EMERGENCY_CONTACT_TELEPHONE_NO IS NOT NULL
				THEN P.EMERGENCY_CONTACT_TELEPHONE_NO
				WHEN P.EMERGENCY_MOBILE_TELEPHONE IS NOT NULL
				THEN P.EMERGENCY_MOBILE_TELEPHONE
				ELSE ''
			END
			,COALESCE(P.EMERGENCY_ADDRESS_1, '')
			,COALESCE(P.EMERGENCY_ADDRESS_2, '')
			,COALESCE(P.EMERGENCY_ADDRESS_3, '')
			,COALESCE(P.EMERGENCY_TOWN, '')
			,COALESCE(P.EMERGENCY_COUNTY, '')
			,COALESCE(P.EMERGENCY_POSTCODE, '')
			,CASE
				WHEN P.EMERGENCY_COUNTRY IS NULL
				THEN 'United Kingdom'
				WHEN P.EMERGENCY_COUNTRY IN ('GBR', 'ENG', 'SCO', 'WAL', 'NI', '')
				THEN 'United Kingdom'
				ELSE XC.[DESCRIPTION]
			END
		)
	) X (PER_REF_NO, CONTACT_NUMBER, ADDR_LINE_1, ADDR_LINE_2, ADDR_LINE_3, ADDR_LINE_4, ADDR_LINE_5, ADDR_LINE_6, ADDRESS_COUNTRY)
	CROSS APPLY
	(
		VALUES
		(
			CASE
				WHEN P.ADDRESS_1 = P.EMERGENCY_ADDRESS_1
					AND P.POSTCODE = P.EMERGENCY_POSTCODE
				THEN 'T'
				WHEN LEN(X.ADDR_LINE_1 + ADDR_LINE_2 + ADDR_LINE_3 + ADDR_LINE_4 + ADDR_LINE_5 + ADDR_LINE_6) = 0
				THEN 'T'
				ELSE 'F'
				END
		)
	) X1 (DEFAULT_ADDRESS_I)
WHERE P.PERSON_STATUS NOT LIKE 'L%'
	AND NOT
	(
		P.EMERGENCY_CONTACT IS NULL
		AND LEN(X.CONTACT_NUMBER) = 0
		AND LEN(ADDR_LINE_1) = 0
		AND LEN(ADDR_LINE_2) = 0
		AND LEN(ADDR_LINE_3) = 0
		AND LEN(ADDR_LINE_4) = 0
		AND LEN(ADDR_LINE_5) = 0
		AND LEN(ADDR_LINE_6) = 0
		AND LEN(REPLACE(ADDRESS_COUNTRY, 'United Kingdom', '')) = 0
	)
--SELECT
--	PER_REF_NO
--	,CONTACT_NAME, DEFAULT_ADDRESS_I, PRIMARY_CONTACT_I, CONTACT_NUMBER, CONTACT_EMAIL
--	,ADDR_LINE_1, ADDR_LINE_2, ADDR_LINE_3, ADDR_LINE_4, ADDR_LINE_5, ADDR_LINE_6
--	,RELATIONSHIP, ADDRESS_COUNTRY
GO
